buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/plugins-release" }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41")
        classpath("io.spring.gradle:propdeps-plugin:0.0.9.RELEASE")
    }
}

plugins {
    id("org.sonarqube") version "2.7"
    id("io.spring.dependency-management") version "1.0.7.RELEASE"
    id("org.jetbrains.kotlin.jvm") version "1.3.41" apply false
    id("com.gradle.build-scan") version "2.2.1"
}

sonarqube {
    properties {
        property "sonar.projectName", "Slack Spring Boot Starter"
        property "sonar.projectKey", "com.kreait.slack-spring-boot-starter"
        property "sonar.host.url", "https://sonarcloud.io/"
        property "sonar.organization", "kreait"
        property "sonar.jacoco.report missing.force.zero", "true"
        property "sonar.pullrequest.provider", "github"
        property "sonar.exclusions", "**/slack-jackson-dto-test-extensions/**,**/slack-jackson-dto/**,**/samples/**"
    }
}

buildScan {
    termsOfServiceUrl = "https://gradle.com/terms-of-service"
    termsOfServiceAgree = "yes"
}


allprojects {

    group = "com.kreait.slack"
    version = rootProject.file("version.txt").text.trim()

    project.ext {
        junitJupiterVersion = "5.4.2"
        junitPlatformVersion = "1.4.2"
        springBootVersion = "2.1.3.RELEASE"
        projectVersion = version
        kotlinVersion = "1.3.21"
    }

    wrapper {
        gradleVersion "5.2.1"
    }

    apply plugin: "io.spring.dependency-management"


    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") {
                bomProperty("kotlin.version", "${kotlinVersion}")
            }
        }
    }

}
subprojects {

    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "jacoco"
    apply plugin: "maven-publish"
    apply plugin: "propdeps"


    publishing {
        repositories {
            maven {
                url "s3://libs.olaph.io"
                credentials(AwsCredentials) {
                    accessKey System.getenv("AWS_ACCESS_KEY_ID")
                    secretKey System.getenv("AWS_SECRET_ACCESS_KEY")
                }
            }

        }
    }

    test {
        useJUnitPlatform()
    }

    /**
     * Replace illegal characters from the module key
     */
    sonarqube {
        String regex = "(.*)/(.*)"
        String projectKey = project.name.replaceAll(regex, "\$1:\$2")
        String sonarModuleKey = rootProject.group + ":" + rootProject.name + ":" + projectKey
        properties {
            property "sonar.moduleKey", sonarModuleKey
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier "sources"
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    jacoco {
        toolVersion = "0.8.3"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(Test) {
        systemProperties = System.properties
        systemProperties['user.dir'] = workingDir
    }

    repositories {
        mavenCentral()
    }

    dependencies {

        testImplementation(group: "com.nhaarman.mockitokotlin2", name: "mockito-kotlin", version: "2.0.0")
        testImplementation(group: "org.junit.jupiter", name: "junit-jupiter", version: "${junitJupiterVersion}")
    }

}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

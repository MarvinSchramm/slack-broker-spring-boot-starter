buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.11")
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.10")
    }
}

plugins {
    id "org.sonarqube" version "2.7"
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id "org.jetbrains.kotlin.jvm" version "1.3.21" apply false
    id "org.asciidoctor.convert" version "1.5.9.2"
}

sonarqube {
    properties {
        property "sonar.projectName", "Slack Broker"
        property "sonar.projectKey", "io.olaph.slack"
        property "sonar.host.url", "https://sonarcloud.io/"
        property "sonar.organization", "olaph-io"
        property "sonar.jacoco.report missing.force.zero", "true"
        property "sonar.pullrequest.provider", "github"
    }
}

asciidoctor {
    sourceDir = file("docs/asciidoc")
    outputDir = file('build/docs')
}

allprojects {

    group = "io.olaph.slack"
    version = "0.8.0.BUILD-SNAPSHOT"

    project.ext {
        junitJupiterVersion = "5.3.1"
        junitPlatformVersion = "1.3.1"
        springBootVersion = "2.1.3.RELEASE"
        projectVersion = version
        kotlinVersion = "1.3.21"
    }

    wrapper {
        gradleVersion "5.2.1"
    }

    apply plugin: "io.spring.dependency-management"


    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") {
                bomProperty("kotlin.version", "${kotlinVersion}")
            }
        }
    }

}
subprojects {

    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "jacoco"
    apply plugin: "maven-publish"


    publishing {
        repositories {
            maven {
                url "s3://libs.olaph.io"
                credentials(AwsCredentials) {
                    accessKey System.getenv("AWS_ACCESS_KEY_ID")
                    secretKey System.getenv("AWS_SECRET_ACCESS_KEY")
                }
            }

        }
    }

    test {
        useJUnitPlatform()
    }

    /**
     * Replace illegal characters from the module key
     */
    sonarqube {
        String regex = "(.*)/(.*)"
        String projectKey = project.name.replaceAll(regex, "\$1:\$2")
        String sonarModuleKey = rootProject.group + ":" + rootProject.name + ":" + projectKey
        properties {
            property "sonar.moduleKey", sonarModuleKey
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier "sources"
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    jacoco {
        toolVersion = "0.8.3"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(Test) {
        systemProperties = System.properties
        systemProperties['user.dir'] = workingDir
    }

    repositories {
        mavenCentral()
    }

    dependencies {

        testImplementation(group: "com.nhaarman.mockitokotlin2", name: "mockito-kotlin", version: "2.0.0")
        testImplementation(group: "org.junit.jupiter", name: "junit-jupiter-params", version: "${junitJupiterVersion}")
        testImplementation(group: "org.junit.jupiter", name: "junit-jupiter-api", version: "${junitJupiterVersion}")
        testRuntime(group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "${junitJupiterVersion}")
        testImplementation(group: "org.junit.platform", name: "junit-platform-runner", version: "${junitPlatformVersion}") {
            exclude(group: "junit", module: "junit")
        }
    }

}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
